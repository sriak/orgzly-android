apply plugin: 'com.android.application'

android {
    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    /*
     * Read properties from the file.
     *
     * This file contains credentials and it is not checked into Git.
     * You should create it -- see "app.properties.sample".
     */
    Properties properties = loadProperties(project.rootProject.file('app.properties'))

    defaultConfig {
        applicationId "com.orgzly"
        minSdkVersion 14 // Ice Cream Sandwich (4.0)
        targetSdkVersion 25 // Nougat (7.1)
        versionCode 58
        versionName "1.4.11"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "DROPBOX_APP_KEY", properties.getProperty("dropbox.app_key", '""')
        resValue "string", "dropbox_app_key_schema", properties.getProperty("dropbox.app_key_schema", '')
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG_DEBUG", "false"
        }

        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"

            buildConfigField "String", "DROPBOX_TOKEN", properties.getProperty("dropbox.token", '""')
        }
    }

    productFlavors {
        premium {
            buildConfigField "boolean", "IS_DROPBOX_ENABLED", "true"
        }

        fdroid {
            /*
             * Disable Dropbox.
             * Properties file which contains the required API key is not included with the code.
             */
            buildConfigField "boolean", "IS_DROPBOX_ENABLED", "false"

            versionNameSuffix ' (fdroid)'
        }
    }

    lintOptions {
        disable 'ResourceType'
        disable 'MissingTranslation'
    }
}

// Warning:Conflict with dependency 'com.android.support:support-annotations'. Resolved versions for app (23.1.1) and test app (23.0.1) differ.
// http://stackoverflow.com/questions/28999124/resolved-versions-for-app-22-0-0-and-test-app-21-0-3-differ
configurations.all {
    resolutionStrategy {
        force "com.android.support:design:$android_support_version"
    }
}

dependencies {
    compile "com.orgzly:org-java:1.0.1-SNAPSHOT"
    // compile project(':org-java') // Used when developing, defined in settings.gradle

    compile "com.dropbox.core:dropbox-core-sdk:$dropbox_core_sdk_version"

    compile "com.googlecode.juniversalchardet:juniversalchardet:1.0.3"

    compile("com.github.machinarius:preferencefragment:$preference_fragment_version") {
        exclude module: 'support-v4'
    }

    compile "com.android.support:support-v4:$android_support_version"
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:cardview-v7:$android_support_version"
    compile "com.android.support:recyclerview-v7:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    androidTestCompile "com.android.support:support-annotations:$android_support_version"

    testCompile "junit:junit:$junit_version"

    androidTestCompile "com.android.support.test:runner:$android_test_version"
    androidTestCompile "com.android.support.test:rules:$android_test_version"

    androidTestCompile "com.android.support.test.espresso:espresso-core:$android_test_espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$android_test_espresso_version"

    androidTestCompile "de.sven-jacobs:loremipsum:$loremipsum_version"
}


def loadProperties(file) {
    Properties properties = new Properties()

    if (file.exists()) {
        properties.load(file.newDataInputStream())
    } else {
        logger.warn("Properties file ${file} does not exist")
    }

    return properties
}